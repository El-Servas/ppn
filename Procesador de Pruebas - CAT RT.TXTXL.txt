Representación Textual del archivo de Excel: "Procesador de Pruebas - CAT RT.xlsm"
Fecha y hora de generación: 13/09/2016 12:32:10 p.m.

Resumen
=======

 * Hojas: 1

 * Nombres Definidos: 2

 * Módulos de Visual Basic: 6

 * Líneas de código VB: 271

Detalle
=======

Hojas:
 [Consola]

Contenido:
[Consola]
[B2] \Procesador de Pruebas para CAT Revolvente Pago Tradicional\
[B3] \v2.0\
[B5] \Requiere los siguientes archivos en la misma carpeta:\
[B7] \* CAT_RT.xlsm\
[B8] \* Data.xlsx\
[B13] \Mapeo de Columnas en Archivo de Datos\
[B15] (MAPEO_START) \Parámetro\
[B16] \i.Monto_Línea\
[B17] \i.Tasa_Anual\
[B18] \i.Tasa_IVA_Interes\
[B19] \i.Comisiones_Iniciales\
[B20] \i.Comisión_Redisposición\
[B21] \i.Plazo_En_Meses\
[C7] \v1.1\
[C8] \Con el formato adecuado\
[C15] \Columna\
[C16] \OFERTA\
[C17] \TASA\
[C18] \IVA\
[C21] \PLAZO\
[D15] \Valor\
[D19] \240\
[D20] \120\
[F13] \Resultados\
[F15] (RESULTADOS_START) \Parámetro\
[Consola]
[F16] \o.CAT\

Nombres:
 [MAPEO_START] =Consola!$B$15
 [RESULTADOS_START] =Consola!$F$15

Nombre del proyecto VB: [VBAProject]

Módulos VBA:
 [ThisWorkbook] Tipo: 100
 [Calcula] Tipo: 1
 [Sheet1] Tipo: 100
 [Utils] Tipo: 1
 [Stopwatch] Tipo: 1
 [Esqueleto] Tipo: 1

Codigo:
[ThisWorkbook] 0 líneas de código.
///--- BEGINNING OF MODULE ---
\\\--- END OF MODULE ---

[Calcula] 171 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Private Const COL_CAT = 8

Private Const PARAM_CAT = "o.CAT"

Private Const ERROR_CONSTANT = "NA*"
Private HuboError As Long
Public Mensaje As String

Private Const FILENAME_CALC_BOOK = "CAT_RT.xlsm"
Private Const FILENAME_DATA_BOOK = "DATA.xlsx"

Private wbCalc As Workbook
Private wsCalc As Worksheet

Private wbData As Workbook
Private wsData As Worksheet

Private ix As Long
Private nReg As Long

Private arParámetrosMapeados() As Variant
Private arParámetrosValor() As Variant

Private arResultados() As Variant
Private ixInicioResultados 'Índice de inicio para los resultados

Private RunWhen As Double
Private Const RUN_INTERVAL_SECONDS = 1
Private Const RUN_PROCEDURE = "ActualizaStatus"

Sub ConfiguraAmbiente()
    Dim stRutaLocal As String
    stRutaLocal = ThisWorkbook.Path & Application.PathSeparator
    
    'Libro de cálculo
    Set wbCalc = Workbooks.Open(stRutaLocal & FILENAME_CALC_BOOK)
    Set wsCalc = wbCalc.Worksheets(1)
    
    'Libro de Datos
    Set wbData = Workbooks.Open(stRutaLocal & FILENAME_DATA_BOOK)
    Set wsData = wbData.Worksheets(1)
    
    ThisWorkbook.Activate
    
    'Filas totales
    nReg = wsData.Cells(1).CurrentRegion.Rows.Count
    HuboError = 0
    Mensaje = ""
    
    ProcesarParametros
End Sub

Sub ProcesarParametros()
    'Creamos listas de parámetros: Valor y Mapeados
    Erase arParámetrosMapeados
    Erase arParámetrosValor
    Erase arResultados
    
    Dim Parámetro, Contenido
    Dim ix, RowOffset
    
    For ix = 2 To [Mapeo_start].CurrentRegion.Rows.Count
        RowOffset = ix - 1
        Parámetro = Range("Mapeo_start").offset(RowOffset, 0)
        Contenido = Range("Mapeo_start").offset(RowOffset, 1) 'Se asume Mapeado
        If Contenido = "" Then
            Contenido = Range("Mapeo_start").offset(RowOffset, 2)
            InsertIntoBiArray arParámetrosValor, Parámetro, Contenido
        Else
            InsertIntoBiArray arParámetrosMapeados, Parámetro, Contenido
        End If
    Next
    
    'Convierte el nombre de columa de los Mapeados a los Índices de columna en el archivo de datos.
    For ix = 1 To UBound(arParámetrosMapeados, 2)
        Contenido = arParámetrosMapeados(2, ix)
        Dim Columna: Columna = wsData.Cells.Rows(1).Find(Contenido).Column
        arParámetrosMapeados(2, ix) = Columna
    Next
    
    'Ahora los Resultados
    With Range("Resultados_start")
        For ix = 2 To .CurrentRegion.Rows.Count
            RowOffset = ix - 1
            Parámetro = .offset(RowOffset, 0)
            InsertIntoBiArray arResultados, Parámetro, ""
        Next
    End With
    
    ixInicioResultados = wsData.Cells(1).CurrentRegion.Columns.Count + 1
End Sub

Sub CorrerPruebas()
    ProgramarActualizacionStatus
    For ix = 2 To nReg
        
        If wsData.Cells(ix, COL_CAT) = "" Then
            Dim jx, Parámetro, Contenido
            
            'Iteramos Parámetros Valor
            For jx = 1 To UBound(arParámetrosValor, 2)
                Parámetro = arParámetrosValor(1, jx)
                Contenido = arParámetrosValor(2, jx)
                wsCalc.Range(Parámetro) = Contenido
            Next
            
            'Iteramos Parámetros Mapeados
            For jx = 1 To UBound(arParámetrosMapeados, 2)
                Parámetro = arParámetrosMapeados(1, jx)
                Contenido = arParámetrosMapeados(2, jx)
                wsCalc.Range(Parámetro) = wsData.Cells(ix, Contenido).Value
            Next
            
            'Plasma Resultado
            wsData.Cells(ix, COL_CAT) = wsCalc.Range(PARAM_CAT)
            
            If IsError(wsData.Cells(ix, COL_CAT)) Then
                HuboError = HuboError + 1
                With wsData.Cells(ix, COL_CAT)
                    .Value = ERROR_CONSTANT
                    .Font.Color = -16776961
                End With
            End If
        End If
        DoEvents
    Next
    If HuboError Then Mensaje = "PRECAUCIÓN: Hubo (" & HuboError & ") registros con error."
End Sub

Sub ApagarAmbiente()
    ApagarActualizacionStatus
 
    wbCalc.Close False
    Set wbCalc = Nothing
    
    wbData.Activate
    Set wbData = Nothing
End Sub

Sub ProgramarActualizacionStatus()
    RunWhen = Now + TimeSerial(0, 0, RUN_INTERVAL_SECONDS)
    Application.OnTime EarliestTime:=RunWhen, Procedure:=RUN_PROCEDURE, Schedule:=True
End Sub

Sub ApagarActualizacionStatus()
    On Error Resume Next
    Application.OnTime EarliestTime:=RunWhen, Procedure:=RUN_PROCEDURE, Schedule:=False
End Sub

Sub ActualizaStatus()
    Dim Status As String
    
    Dim Porcentaje: Porcentaje = ix / nReg * 100
    Dim SegundosTranscurridos As Long: SegundosTranscurridos = Stopwatch.Ellapsed
    Dim TotalEstimado As Long: TotalEstimado = 0: If Porcentaje > 0 Then TotalEstimado = SegundosTranscurridos * 100 / Porcentaje
    Dim SegundosRestantes As Long: SegundosRestantes = TotalEstimado - SegundosTranscurridos
    Dim Velocidad: Velocidad = ix / SegundosTranscurridos
    
    Status = "Fila #" & FormatNumber(ix, 0)
    Status = Status & " | " & "" & FormatNumber(Porcentaje, 0) & "%"
    Status = Status & " | " & "" & FormatNumber(Velocidad, 0) & "f/s"
    Status = Status & " | " & "Trans " & FriendlySeconds(SegundosTranscurridos)
    Status = Status & " | " & "Resta " & FriendlySeconds(SegundosRestantes)
    Status = Status & " | " & "Total " & FriendlySeconds(TotalEstimado)
    
    Application.StatusBar = Status
    
    ProgramarActualizacionStatus
End Sub
\\\--- END OF MODULE ---

[Sheet1] 2 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

\\\--- END OF MODULE ---

[Utils] 59 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

'Regresa una cadena con una duración "amigable"
Function FriendlySeconds(Seconds As Long) As String
    Dim strResultado As String: strResultado = FormatNumber(Seconds, 0) + "s"
    If Seconds > 60 Then strResultado = strResultado & " (" + CStr(Int(Seconds / 60)) & ":" & PadCeros(Seconds - (60 * Int(Seconds / 60))) & "m)"
    FriendlySeconds = strResultado
End Function

'Rellena ceros a la izquierda.
Function PadCeros(s)
    PadCeros = Right(String(2, "0") & s, 2)
End Function

'Incrementa el tamaño de un arreglo bidimensional y agrega Key y Value
Sub InsertIntoBiArray(arr() As Variant, Key, Value)
    Dim NewSize: NewSize = 1
    On Error Resume Next: NewSize = UBound(arr, 2) + 1: On Error GoTo 0
    
    ReDim Preserve arr(1 To 2, 1 To NewSize)
    arr(1, NewSize) = Key
    arr(2, NewSize) = Value
End Sub

'==============================================================================

Sub test()
    'Tabla de parámetros
    Dim Rg As Range, cl As Range
    Set Rg = Range("MAPEO_START").CurrentRegion
    Set Rg = Rg.Resize(Rg.Rows.Count - 1)
    Set Rg = Rg.offset(1)
    Set Rg = Rg.Columns(1)
    'Rg.Select

    For Each cl In Rg.Cells
        Dim Parametro As String: Parametro = cl.Value
        Dim Tipo, Valor
        If cl.offset(0, 1) <> "" Then
            Tipo = "Mapeado"
            Valor = cl.offset(0, 1)
        Else
            Tipo = "Valor"
            Valor = cl.offset(0, 2)
        End If
        Debug.Print Parametro, Tipo, Valor
    Next
End Sub

Sub UseDA()
    Dim arr() As Variant
    InsertIntoBiArray arr, "TestKey", "TestValue"
    InsertIntoBiArray arr, "TestKey2", "TestValue2"
    Dim ix: For ix = 1 To UBound(arr, 2)
        Debug.Print arr(1, ix), arr(2, ix)
    Next
End Sub


\\\--- END OF MODULE ---

[Stopwatch] 11 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Private StartTime As Date

Sub Start()
    StartTime = Now
End Sub

Function Ellapsed() As Long
    Ellapsed = DateDiff("s", StartTime, Now)
End Function
\\\--- END OF MODULE ---

[Esqueleto] 28 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Sub Principal()
On Error GoTo err_hand
    Application.EnableCancelKey = xlErrorHandler
    Application.ScreenUpdating = False
    
    ConfiguraAmbiente
    
    Stopwatch.Start
    CorrerPruebas
    
err_hand:
    ApagarAmbiente
    
    Application.ScreenUpdating = True
    Application.StatusBar = False
    
    AppActivate ThisWorkbook.Application.Caption
    
    If Err.Number = 0 Then
        Dim Msg: Msg = "Proceso terminado en " & FriendlySeconds(Stopwatch.Ellapsed)
        If Calcula.Mensaje <> "" Then Msg = Msg & vbCrLf & vbCrLf & Calcula.Mensaje
        MsgBox Msg
    Else
        Debug.Print Err.Number & " - " & Err.Description
    End If
End Sub
\\\--- END OF MODULE ---


** Fin del proceso de exportación. Proceso realizado en 01 sec.  **
Lib_ExportToTxt [ExportToTxt.xls] v2.13.1
