Representación Textual del archivo de Excel: "Procesador de Pruebas - CAT RT.xlsm"
Fecha y hora de generación: 08/09/2016 08:05:36 a.m.

Resumen
=======

 * Hojas: 1

 * Nombres Definidos: 0

 * Módulos de Visual Basic: 5

 * Líneas de código VB: 151

Detalle
=======

Hojas:
 [Sheet1]

Contenido:
[Sheet1]

Nombres:

Nombre del proyecto VB: [VBAProject]

Módulos VBA:
 [ThisWorkbook] Tipo: 100
 [Calcula] Tipo: 1
 [Sheet1] Tipo: 100
 [Utils] Tipo: 1
 [Stopwatch] Tipo: 1

Codigo:
[ThisWorkbook] 0 líneas de código.
///--- BEGINNING OF MODULE ---
\\\--- END OF MODULE ---

[Calcula] 125 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Private Const COL_COMISIÓN = 2
Private Const COL_MONTO = 3
Private Const COL_TASA = 4
Private Const COL_PLAZO = 5
Private Const COL_IVA = 6
Private Const COL_CAT = 8

Private Const PARAM_MONTO = "i.Monto_Línea"
Private Const PARAM_TASA = "i.Tasa_Anual"
Private Const PARAM_IVA = "i.Tasa_IVA_Interes"
Private Const PARAM_COMISIÓN = "i.Comisiones_Iniciales"
Private Const PARAM_COM_REDISP = "i.Comisión_Redisposición"
Private Const PARAM_PLAZO = "i.Plazo_En_Meses"
Private Const PARAM_CAT = "o.CAT"

Private Const FILENAME_CALC_BOOK = "CAT_RT.xlsm"
Private Const FILENAME_DATA_BOOK = "DATA.xlsx"

Private wsCalc As Worksheet
Private wsSrc As Worksheet

Dim ix As Long 'Indice de la fila que se está procesando.
Dim nReg As Long 'Número de filas totales.

Private RunWhen As Double
Private Const cRunIntervalSeconds = 1
Private Const cRunWhat = "ActualizaStatus"

Sub Principal()
On Error GoTo err_hand
    Application.EnableCancelKey = xlErrorHandler

    Dim stRutaLocal As String
    stRutaLocal = ThisWorkbook.Path & Application.PathSeparator
    
    'Abrir Libro de cálculo, obtener referencia a la primera hoja
    Dim wbCalc As Workbook
    Set wbCalc = Workbooks.Open(stRutaLocal & FILENAME_CALC_BOOK)
    Set wsCalc = wbCalc.Worksheets(1)
    
    'Abrir Libro de Datos
    Dim wbData As Workbook
    Set wbData = Workbooks.Open(stRutaLocal & FILENAME_DATA_BOOK)
    Set wsSrc = wbData.Worksheets(1)
    
    'Fix?
    ThisWorkbook.Activate
    Application.ScreenUpdating = False
    
    'Filas totales
    nReg = wsSrc.Cells(1).CurrentRegion.Rows.Count
    
    'Ejecutar pruebas
    Stopwatch.Start
    CorrerPruebas
    
err_hand:
    ApagarEvento
 
    'Cerrar Libro de cálculo.
    wbCalc.Close False
    Set wbCalc = Nothing
    
    wbData.Activate
    Set wbData = Nothing
    
    Application.ScreenUpdating = True
    Application.StatusBar = False
    
    If Err.Number = 0 Then
        MsgBox "Proceso terminado en " & FriendlySeconds(Stopwatch.Ellapsed) & vbCrLf & _
                "Grabe el Libro!"
    Else
        Debug.Print Err.Number & " - " & Err.Description
    End If
    
End Sub

Sub CorrerPruebas()
    ProgramarEvento
    For ix = 2 To nReg
        
        If wsSrc.Cells(ix, COL_CAT) = "" Then
            wsCalc.Range(PARAM_COMISIÓN) = wsSrc.Cells(ix, COL_COMISIÓN).Value
            wsCalc.Range(PARAM_MONTO) = wsSrc.Cells(ix, COL_MONTO).Value
            wsCalc.Range(PARAM_TASA) = wsSrc.Cells(ix, COL_TASA) / 100
            wsCalc.Range(PARAM_PLAZO) = wsSrc.Cells(ix, COL_PLAZO)
            wsCalc.Range(PARAM_IVA) = wsSrc.Cells(ix, COL_IVA)
            
            wsSrc.Cells(ix, COL_CAT) = wsCalc.Range(PARAM_CAT)
        End If
        DoEvents
    Next
End Sub

Sub ProgramarEvento()
    RunWhen = Now + TimeSerial(0, 0, cRunIntervalSeconds)
    Application.OnTime EarliestTime:=RunWhen, Procedure:=cRunWhat, Schedule:=True
End Sub
Sub ApagarEvento()
    On Error Resume Next
    Application.OnTime EarliestTime:=RunWhen, Procedure:=cRunWhat, Schedule:=False
End Sub

Sub ActualizaStatus()
    Dim Status As String
    
    Dim Porcentaje: Porcentaje = ix / nReg * 100
    Dim SegundosTranscurridos As Long: SegundosTranscurridos = Stopwatch.Ellapsed
    Dim TotalEstimado As Long: TotalEstimado = 0: If Porcentaje > 0 Then TotalEstimado = SegundosTranscurridos * 100 / Porcentaje
    Dim SegundosRestantes As Long: SegundosRestantes = TotalEstimado - SegundosTranscurridos
    Dim Velocidad: Velocidad = ix / SegundosTranscurridos
    
    Status = "Fila #" & FormatNumber(ix, 0)
    Status = Status & " | " & "" & FormatNumber(Porcentaje, 0) & "%"
    Status = Status & " | " & "" & FormatNumber(Velocidad, 0) & "f/s"
    Status = Status & " | " & "Transcurridos: " & FriendlySeconds(SegundosTranscurridos)
    Status = Status & " | " & "Restante Estimado: " & FriendlySeconds(SegundosRestantes)
    Status = Status & " | " & "Total Estimado: " & FriendlySeconds(TotalEstimado)
    
    Application.StatusBar = Status
    ProgramarEvento
End Sub
\\\--- END OF MODULE ---

[Sheet1] 2 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

\\\--- END OF MODULE ---

[Utils] 13 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

'Regresa una cadena con una duración "amigable"
Function FriendlySeconds(Seconds As Long) As String
    Dim strResultado As String: strResultado = FormatNumber(Seconds, 0) + "s"
    If Seconds > 60 Then strResultado = strResultado & " (" + CStr(Int(Seconds / 60)) & ":" & PadCeros(Seconds - (60 * Int(Seconds / 60))) & "m)"
    FriendlySeconds = strResultado
End Function

'Rellena ceros a la izquierda.
Function PadCeros(s)
    PadCeros = Right(String(2, "0") & s, 2)
End Function
\\\--- END OF MODULE ---

[Stopwatch] 11 líneas de código.
///--- BEGINNING OF MODULE ---
Option Explicit

Private StartTime As Date

Sub Start()
    StartTime = Now
End Sub

Function Ellapsed() As Long
    Ellapsed = DateDiff("s", StartTime, Now)
End Function
\\\--- END OF MODULE ---


** Fin del proceso de exportación. Proceso realizado en 00 sec.  **
Lib_ExportToTxt [ExportToTxt.xls] v2.13.1
